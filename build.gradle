// Top-level build file where you can add configuration options common to all sub-projects/modules.

buildscript {
    ext.buildConfig = [
        'compileSdk': 28,
        'minSdk': 22,
        'targetSdk': 28,
        'version': [
            'major': 1,
            'minor': 1,
            'patch': 0,
            'build': 0,
        ],
    ]

    ext.buildConfig.version['name'] = "${buildConfig.version.major}.${buildConfig.version.minor}.${buildConfig.version.patch}"
    ext.buildConfig.version['fullName'] = "${buildConfig.version.name}.${buildConfig.version.build}"
    ext.buildConfig.version['code'] = buildConfig.version.major * 1000000 + buildConfig.version.minor * 10000 + buildConfig.version.patch * 100 + buildConfig.version.build

    ext.versions = [
        'anko': '0.10.8',
        'kotlin': '1.3.60',
        'retrofit': '2.5.0',
        'okhttp': '3.12.1',
        'dagger': '2.20'
    ]

    ext.deps = [
        kotlin: [
            stdlib: [
                jdk: "org.jetbrains.kotlin:kotlin-stdlib-jdk7:${versions.kotlin}",
            ],
        ],
        androidx: [
            appCompat: 'androidx.appcompat:appcompat:1.1.0',
            fragment: 'androidx.fragment:fragment-ktx:1.1.0',
            localBroadcastManager: 'androidx.localbroadcastmanager:localbroadcastmanager:1.0.0',
            core: 'androidx.core:core-ktx:1.1.0',
            recyclerView: 'androidx.recyclerview:recyclerview:1.0.0',
            viewModel: [
                    ktx: 'androidx.lifecycle:lifecycle-viewmodel-ktx:2.1.0',
                    extensions: 'androidx.lifecycle:lifecycle-extensions:2.1.0'
            ],
        ],
        anko: [
            sqlite: "org.jetbrains.anko:anko-sqlite:${versions.anko}",
            design: "org.jetbrains.anko:anko-design:${versions.anko}"
        ],
        okhttp: [
            client: "com.squareup.okhttp3:okhttp:${versions.okhttp}",
            logging: "com.squareup.okhttp3:logging-interceptor:${versions.okhttp}",
        ],
        retrofit: [
            client: "com.squareup.retrofit2:retrofit:${versions.retrofit}",
            gsonConverter: "com.squareup.retrofit2:converter-gson:${versions.retrofit}",
            adapterRxJava: "com.squareup.retrofit2:adapter-rxjava2:${versions.retrofit}"
        ],
        dagger: [
            runtime: "com.google.dagger:dagger:${versions.dagger}",
            compiler: "com.google.dagger:dagger-compiler:${versions.dagger}"
        ],
        crashlytics: 'com.crashlytics.sdk.android:crashlytics:2.10.1@aar',
        sqlbrite: 'com.squareup.sqlbrite2:sqlbrite:2.0.0',
        rxjava: 'io.reactivex.rxjava2:rxandroid:2.1.1',
        debugDb: 'com.amitshekhar.android:debug-db:1.0.6',
        glide: 'com.github.bumptech.glide:glide:3.8.0',
        jsr310: 'com.jakewharton.threetenabp:threetenabp:1.1.1',
        timber: 'com.jakewharton.timber:timber:4.7.1',
        material: 'com.google.android.material:material:1.1.0-alpha07',
        crop: 'com.soundcloud.android:android-crop:1.0.1@aar'
    ]

    repositories {
        google()
        jcenter()
        
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:3.5.2'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:${versions.kotlin}"
        // NOTE: Do not place your application dependencies here; they belong
        // in the individual module build.gradle files
    }
}

allprojects {
    repositories {
        google()
        jcenter()
        
    }
}

task clean(type: Delete) {
    delete rootProject.buildDir
}
